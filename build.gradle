plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'

}

group = 'com.codeit'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jacoco {
    toolVersion = "0.8.10"
    reportsDirectory = layout.buildDirectory.dir('jacocoReport')
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'

    // Monitoring
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    implementation 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'

    /* QueryDSL */
    implementation 'com.querydsl:querydsl-jpa:5.1.0:jakarta'
    annotationProcessor 'com.querydsl:querydsl-apt:5.1.0:jakarta'
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    /* Batch, Retry */
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    implementation 'org.springframework.retry:spring-retry'               // @Retryable
    implementation 'org.springframework:spring-aspects'

    // mapstruct mapper의존성
    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

    //User
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
    //Security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.batch:spring-batch-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    //구글 제미나이 API
    implementation 'com.google.genai:google-genai:1.0.0'

    //AWS S3 SDK 의존성
    implementation 'software.amazon.awssdk:s3:2.31.7'


}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport //테스트가 끝나고 Jacoco 리포트 작성
}


//jacoco 리포트 설정
jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
        html.required = true
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, excludes: [
                            //분석 리포트에서 제외할 클래스를 설정합니다.
                            '**/Q*.class',                        // QueryDSL Q클래스
                            '**/generated/**',                   // generated 패키지 제외
                            '**/mapper',              // 모든 mapper 패키지 제외
                            '**/entity', //entity 제외
                            '**/dto', //dto 제외
                    ])
                })
        )
    }
    finalizedBy 'jacocoTestCoverageVerification'
}

/* ===== QueryDSL 플러그인 전용 설정 ===== */
def generated = 'build/generated/querydsl'


/* IDE·빌드 양쪽 모두 Q클래스를 소스 루트로 인식하도록 */
tasks.withType(JavaCompile).configureEach {
    options.generatedSourceOutputDirectory = file(generated)
}

sourceSets {
    main.java.srcDirs += "$projectDir/$generated"
}

clean {
    delete file('build/generated/querydsl')
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from('src/main/resources') {
        include '**/*.sql'
    }
}
